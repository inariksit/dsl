ModuleDecl. Module ::= "module" ObjAttr "where";
Toplevel.  Tops      ::= [Toplevels];
rules      Toplevels ::= Module | Import | Pragma 
                       | Rule | Entity | Scenario; -- Modal
terminator Toplevels ";";

layout toplevel;
layout "WITH", "TRACE", "WHERE" ;
entrypoints Tops;
comment "//" ;
comment "--" ;
comment "/*" "*/" ;
rules Pragma ::= "pragma" CommaList ;
Import. Import ::= "import" ObjAttr ;

rules      RuleDef   ::= RuleNum | RuleID | RuleName
                       | RuleNum   RuleID 
                       | RuleNum            RuleName
                       |           RuleID   RuleName
                       | RuleNum   RuleID   RuleName ;
rules      RuleID    ::= ObjAttr ;
RName.     RuleName  ::= LangStrings ;
RNum.      RuleNum   ::= Integer;

Rule.      Rule      ::= "RULE" RuleDef RuleBody;
RBNoop.    RuleBody  ::= "NOOP";

-- DEEM Potato IS noble WHEN ownersAreNoble
RBis.     RuleBody  ::= "DEEM"  ObjAttr     "IS"  [ObjAttrAnd] WhenLimb WhereLimb;
RBare.    RuleBody  ::= "DEEM" [ObjAttrAnd] "ARE" [ObjAttrAnd] WhenLimb WhereLimb;

-- DEEM Potato ISA DiscountItem WHEN CurrentDate ~ Weekend
RBisa.    RuleBody  ::= "DEEM"  ObjAttr     "ISA" [ObjAttrAnd] WhenLimb WhereLimb;

-- DEEM Potato HAS discount == 20% WHEN CurrentDate WITHIN 1 week OF ExpirationDate
RBhas.    RuleBody  ::= "DEEM"  ObjAttr     "HAS" [ObjAttrAnd] WhenLimb WhereLimb;

separator nonempty ObjAttrAnd "AND";
OBAnd.    ObjAttrAnd ::= ObjAttr ;

rules      WhenLimb ::= | "WHEN" PredExpr ;

-- ... WHEN  
PEUnify.   PredExpr  ::= UnifyExpr;
PEAnd.     PredExpr  ::= PredExpr "AND" PredExpr;
PEOr.      PredExpr  ::= PredExpr "OR"  PredExpr;
PEME.      PredExpr1 ::= MatchExpr;
PELikely.  PredExpr2 ::= Likelihood PredExpr ;
_.         PredExpr2 ::= "(" PredExpr ")";
coercions PredExpr 2;
-- do we really need to say coercions?
rules      UnifyExpr ::= [UnifyElem] | PlainVal;
rules      VarExpr   ::= ObjAttr | PlainVal;
rules      PlainVal  ::= Numberlike | String
                       | BraceList | JunctionList | ArrayOfBraces
                       | DateTime | DateTime "to" DateTime | Boolean;
rules      UnifyElem ::= ObjAttrElem
                      |  UnifyBracket
                      |  UnifyStar;
separator nonempty UnifyElem ".";
rules      UnifyStar ::= "*" ;
rules      UnifyBracket ::= "[" CommaList "]" ;
ME_OA_JL.  MatchExpr  ::= UnifyExpr "~" JunctionList ;
ME_OA_Bin. MatchExpr  ::= UnifyExpr BinOp UnifyExpr ;

-- similar to CLP #=
rules      BinOp      ::= "+" | "-" | "*" | "/" | "<" | ">" | "<=" | ">=" | "==" | "IS" | "ISA" | "ARE";

rules      Numeric    ::= Integer | Double ;
rules      Numberlike ::=        Numeric
                        | "$"    Numeric
                        | CurrID Numeric
                        |        Numeric "%" ;
rules      Likelihood  ::= | "LIKELY" | "UNLIKELY";
JL_And.    JunctionList ::= "["   AndList "]" ;
JL_Or.     JunctionList ::= "["    OrList "]" ;
JL_Xor.    JunctionList ::= "["   XorList "]" ;
JL_Comma.  JunctionList ::= "[" CommaList "]" ;

CommaList.  CommaList ::= [CommaElem];
rules      CommaElem ::= ObjAttr | String;
separator nonempty CommaElem ",";

AndList.   AndList ::= [AndElem];
rules      AndElem ::= ObjAttr | String;
separator nonempty AndElem "&";

OrList.    OrList ::= [OrElem];
rules      OrElem ::= ObjAttr | String;
separator nonempty OrElem "|";

XorList.   XorList ::= [XorElem];
rules      XorElem ::= ObjAttr | String;
separator nonempty XorElem "^";


rules      ObjAttrElem  ::= Ident | UIdent ;            -- Foo

OA_dots.   ObjAttr      ::= [ObjAttrElem];              -- Foo.Bar.Baz
OA_method. ObjAttr      ::= ObjAttr "(" CommaList ")" ; -- Foo.Bar(Baz, Quux)
separator nonempty ObjAttrElem ".";


REntity.   Entity ::= "ENTITY" ObjAttr
                         GivenLimb
                         EntityDef;
rules      EntityDef ::= "ISA" ObjAttr WithLimb Asof WhereLimb
                       | "HAS"         WithLimb Asof WhereLimb;

rules      GivenLimb ::= | "GIVEN"  GivenExpr ;
rules      GivenExpr ::=   ObjAttr
                         | ObjAttr HavingLimb ;
rules HavingLimb ::= "HAVING"  PredExpr;

rules      WithLimb  ::= | "WITH" "{" [WithIn] "}";
rules      WithIn    ::= [Assignment] | TraceExpr;
separator nonempty WithIn ";";
rules      TraceExpr ::= "TRACE" "{" [LogEvent] "}";
rules      LogEvent ::= Iso8601 ObjAttr ObjAttr BlahBlah ;
separator nonempty LogEvent ";";
separator nonempty Blah "";
rules      BlahBlah ::= | [Blah] ;
rules      Blah     ::= Numberlike | ObjAttr | JunctionList | BraceList;
rules     WhereLimb ::= | "WHERE" "{" [WhereIn] "}" ;
rules     WhereIn   ::= ObjAttr "ISA" ObjAttr WithLimb
                      | Assignment;
separator nonempty WhereIn ";";
Asof.      Asof      ::= "ASOF" DateTime ;
AsofNull.  Asof      ::= ;
rules      DateTime  ::= Iso8601 | "PRESENT" | "NOW" ;
rules      Iso8601   ::= YYYYMMDD | YYYYMMDDTHHMM;

token YYYYMMDD      ( digit digit digit digit '-'? digit digit '-'? digit digit );
token YYYYMMDDTHHMM ( digit digit digit digit '-'? digit digit '-'? digit digit 'T' digit digit digit digit );
token UIdent (upper (letter | digit | '_')*) ;
rules      UIdentList ::= [UIdentElem];
rules      UIdentElem ::= UIdent;
separator nonempty UIdentElem ".";

BTrue.     Boolean    ::= TrueBool; -- later, maybe | "yes"
BFalse.    Boolean    ::= FalseBool;

token TrueBool  ["Tt"] ["Rr"] ["Uu"] ["Ee"] ;
token FalseBool ["Ff"] ["Aa"] ["Ll"] ["Ss"] ["Ee"];
rules BraceList     ::= "{" [Assignment] "}" ;
rules  Assignment   ::= ObjAttr "=" VarExpr;
rules ArrayOfBraces ::= "[" [BraceList] "]";
separator nonempty Assignment  "," ;
separator nonempty BraceList "," ;
RScenario. Scenario ::= "SCENARIO" ObjAttr WithLimb TraceExpr Asof WhereLimb ;

EString.      NormalString ::= String ;
ELangStrings. LangStrings  ::= [LangString] ;
    separator nonempty LangString " ";
ELangString.  LangString   ::= LangID NormalString ;
rules         LangID       ::= ":" [LangLabel] ":";
rules         LangLabel    ::= Ident ;
    separator nonempty LangLabel "," ;

rules         CurrID       ::= ":" CurrLabel ":";
rules         CurrLabel    ::= UIdent ;
